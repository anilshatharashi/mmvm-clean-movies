plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.example.mvvmclean.anilshatharashi"
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    def keys = new Properties()
    file("../keys.properties").withInputStream {
        stream -> keys.load(stream)
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"" + keys.BASE_URL_DEBUG + "\"" // this can be moved under the product flavors
            buildConfigField "String", "BASE_URL_MOVIES_POSTERS", "\"" + keys.BASE_URL_MOVIES_POSTERS_DEBUG + "\"" // this can be moved under the product flavors
            buildConfigField "String", "API_KEY", "\"" + keys.API_KEY_DEBUG + "\"" // this can be moved under the product flavors
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"" + keys.BASE_URL_RELEASE + "\""
            buildConfigField "String", "BASE_URL_MOVIES_POSTERS", "\"" + keys.BASE_URL_MOVIES_POSTERS_RELEASE + "\""
            buildConfigField "String", "API_KEY", "\"" + keys.API_KEY_RELEASE + "\""
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

dependencies {
    implementation "androidx.core:core-ktx:$ktxVersion"
    implementation "androidx.fragment:fragment-ktx:1.3.4"

    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$coroutinesVersion"

    // LiveData-ViewModel
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycleVersion"
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"
    // LifeCycleScope or ViewModelScopes
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha01'

    //hilt
    implementation "com.google.dagger:hilt-android:$hiltVersion"
//    implementation "androidx.lifecycle:lifecycle-process:$lifecycleVersion"
    kapt "com.google.dagger:hilt-android-compiler:$hiltVersion"
    //hilt-androidx
    kapt "androidx.hilt:hilt-compiler:$hiltAndroidxVersion"

    // Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitGsonConverterVersion"
    implementation 'com.google.code.gson:gson:2.8.6'
    // OkHttp Logging interceptor
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    testImplementation "io.mockk:mockk:$mockkVersion"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}